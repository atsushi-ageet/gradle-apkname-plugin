buildscript {
    ext {
        kotlin_version = '1.4.10'
        dokka_version = '0.9.17'
        jgit_version = '4.8.0.201706111038-r'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'signing'
    id 'maven-publish'
}

archivesBaseName = 'apkname'
group 'com.ageet.apkname'
version '0.2.0'

apply plugin: 'kotlin'

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.tools.build:gradle:4.1.0'
    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'org.jetbrains.dokka'

dokka {
    moduleName = archivesBaseName
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    sourceDirs = sourceSets.main.kotlin.srcDirs
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    from dokka.outputDirectory
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = archivesBaseName
                description = 'Auto naming for android apk'
                url = 'https://github.com/atsushi-ageet/gradle-apkname-plugin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'atsushi'
                        name = 'Atsushi Yamauchi'
                        email = 'atsushi@ageet.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/atsushi-ageet/gradle-apkname-plugin.git'
                    developerConnection = 'scm:git:ssh:git@github.com:atsushi-ageet/gradle-apkname-plugin.git'
                    url = 'https://github.com/atsushi-ageet/gradle-apkname-plugin/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username project.properties["sonatype.user"]
                password project.properties["sonatype.password"]
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

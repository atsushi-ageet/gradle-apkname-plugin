group 'com.ageet'
version '0.1.1'

def artifactBaseName = 'apkname'

buildscript {
    ext {
        kotlin_version = '1.1.51'
        dokka_version = '0.9.15'
        jgit_version = '4.8.0.201706111038-r'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.2'
    }
}

apply plugin: 'kotlin'

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.tools.build:gradle:3.0.0'
    compile "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    baseName = artifactBaseName
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.properties["bintray.user"]
    key = project.properties["bintray.key"]
    publications = ['defaultPublication']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'gradle-apkname-plugin'
        desc = 'Auto naming for android apk'
        userOrg = user
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/atsushi-ageet/gradle-apkname-plugin'
        issueTrackerUrl = 'https://github.com/atsushi-ageet/gradle-apkname-plugin/issues'
        vcsUrl = 'https://github.com/atsushi-ageet/gradle-apkname-plugin.git'

        githubRepo = 'atsushi-ageet/gradle-apkname-plugin'
        githubReleaseNotesFile = 'README.md'

        version {
            name = project.version
            vcsTag = 'v0.0.6'
            attributes = ['gradle-plugin': 'com.ageet.apkname:com.ageet:apkname']
        }
    }
}

apply plugin: 'org.jetbrains.dokka'

dokka {
    moduleName = artifactBaseName
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    sourceDirs = sourceSets.main.kotlin.srcDirs
}

task sourcesJar(type: Jar) {
    baseName = artifactBaseName
    classifier = 'sources'
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    baseName = artifactBaseName
    classifier = 'javadoc'
    from dokka.outputDirectory
}

apply plugin : 'maven-publish'

publishing {
    publications {
        defaultPublication(MavenPublication) {
            artifactId artifactBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

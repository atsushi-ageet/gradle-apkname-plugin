buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'com.ageet.apkname'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28

        applicationId "com.ageet.gradle.plugin.apkname.example"
        versionCode 1
        versionName "1.0"
    }

    apkName {
        format { "apkname-example-${it.variant.productFlavors*.name.join("-")}-${it.buildType}-${it.versionName}-${it.gitShortHash}" }
    }

    flavorDimensions "dimension1", "dimension2"

    productFlavors {
        f1 {
            dimension "dimension1"
        }
        f2 {
            dimension "dimension1"
        }
        f3 {
            dimension "dimension2"
        }
        f4 {
            dimension "dimension2"
        }
    }
}

task showApkName {
    doLast {
        android.applicationVariants.each { variant ->
            variant.outputs*.outputFile.collect {
                "${variant.name}: ${it.name}"
            }.each(logger.&quiet)
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.ageet:apkname:0.0.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.ageet.apkname'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25

        applicationId "com.ageet.gradle.plugin.apkname.example"
        versionCode 1
        versionName "1.0"
    }

    apkName {
        releaseOnly true
        format "apkname-example-${flavorName}-${versionName}-${gitShortHash}"
    }

    productFlavors {
        f1
        f2
    }
}

task showApkName {
    doLast {
        android.applicationVariants.each { variant ->
            variant.outputs*.outputFile.collect {
                "${variant.name}: ${it.name}"
            }.each(logger.&quiet)
        }
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.1'
    }
}

plugins {
    id 'com.android.application'
    id 'com.ageet.apkname'
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30

        applicationId "com.ageet.gradle.plugin.apkname.example"
        versionCode 1
        versionName "1.0"
    }

    apkName {
        format { "apkname-example-${it.variant.productFlavors*.name.join("-")}-${it.buildType}-${it.versionName}-${it.gitShortHash}" }
    }

    flavorDimensions "dimension1", "dimension2"

    productFlavors {
        f1 {
            dimension "dimension1"
        }
        f2 {
            dimension "dimension1"
        }
        f3 {
            dimension "dimension2"
        }
        f4 {
            dimension "dimension2"
        }
    }
}

task showApkName {
    doLast {
        android.applicationVariants.each { variant ->
            variant.outputs*.outputFile.collect {
                "${variant.name}: ${it.name}"
            }.each(logger.&quiet)
        }
    }
}

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta3'
        classpath 'com.ageet:apkname:0.0.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.ageet.apkname'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26

        applicationId "com.ageet.gradle.plugin.apkname.example"
        versionCode 1
        versionName "1.0"
    }

    apkName {
        format "apkname-example-$flavorName-$buildType-$versionName-$gitShortHash"
    }

    flavorDimensions "dimension1", "dimension2"

    productFlavors {
        f1 {
            dimension "dimension1"
        }
        f2 {
            dimension "dimension1"
        }
        f3 {
            dimension "dimension2"
        }
        f4 {
            dimension "dimension2"
        }
    }
}

task showApkName {
    doLast {
        android.applicationVariants.each { variant ->
            variant.outputs*.outputFile.collect {
                "${variant.name}: ${it.name}"
            }.each(logger.&quiet)
        }
    }
}
